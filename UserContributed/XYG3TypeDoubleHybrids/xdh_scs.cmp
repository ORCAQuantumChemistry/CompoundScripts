#
# XYGJ-OS with RI-MP2
#
# Final energy : EDH
#
Variable molecule;
Variable charge = 0;
Variable mult = 1;
Variable xc = "xygjos";
Variable basis = "def2-svp";
Variable auxbas = "def2-svp/c";
Variable dlpno = "false";

Variable acm[3] ;
Variable scalldac;
Variable pt2_coeff;
Variable ss = 1.0;
Variable os = 1.0;
Variable Enscf;
Variable Ept2;
Variable Edh;

if (xc = "xygjos") then
 pt2_coeff = 0.4364;
 ss = 0.0;
 acm[0] = 0.7731;
 acm[1] = 0.0 ;
 acm[2] = 0.2754;
 scalldac = 0.5063; 
endif

NewStep
  !b3lyp_g &{basis}
  !noautostart
  *xyzfile &{charge} &{mult} &{molecule}
StepEnd
Alias scf

ReadMOs(scf);
NewStep
  !b3lyp_g &{basis}
  !nopop 
  %scf 
    maxiter 1 
    ignoreconv true
  end
  %method
    Functional b3lyp_g
    ACM = &{acm[0]}, &{acm[1]},&{acm[2]}
    ScalLDAC = &{scalldac}
  end
StepEnd
Enscf.ReadProperty(propertyName="SCF_Energy");

ReadMOs(scf);
NewStep
  !RI-MP2 &{basis} &{auxbas}
  !noiter nopop
  !nofrozencore
  %mp2
    dlpno &{dlpno}
    doscs true
    ps &{os}
    pt &{ss}
  end
StepEnd
Ept2.ReadProperty(propertyName="MP2_Corr_Energy");

Edh = (Enscf + Ept2*pt2_coeff);
#print("%s Results", xc);
#print("Total energy: %lf", Edh);

print("-------------   Results  ----------------\n");
print("pt2 coef  : %20.8lf\n", pt2_coeff);
print("Enscf     : %20.8lf\n", Enscf);
print("Ept2      : %20.8lf\n", Ept2);
print("Edh       : %20.8lf\n", Edh);

EndRun
