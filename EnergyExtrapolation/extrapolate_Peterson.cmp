# Author: Dimitrios G. Liakos and Frank Neese
# Date  : June of 2024
# Update: June of 2025
#
# This is a three-point energy extrapolation scheme for the total
#   energy based on:
#
#  'Benchmark calculations with correlated molecular wave functions. 
#   IV. The classical barrier height of the H+H2-4-H2+H reaction', 
#   Kirk A. Peterson , David E. Woon, and Thorn H. Dunning, Jr.
#       J. Chem. Phys. 100 (10), 15 May 1994 
#
#   that uses the energy function:
#
#   E(X) = E(CBS)+ Bexp(-(x-1))+Cexp(-(x-1)^2)
# 
#   for the total energy.
#   Using this form and DZ,TZ and QZ basis sets the E(CBS) is:
# 
#    E(CBS)= [eE(TZ)-E(DZ)+D(-exp(-1)E(TZ)+E(QZ)) ]/[exp(-1)-D(exp(-1)-1)]
# 
#    with        D= [exp(-1)-exp(-5)]/[exp(-9)-exp(-3)]
#
#  In the current implementation we perform the following steps:
#
#  Step 1: Perform a calculation with basis set with cardinal number X   (here aug-cc-pVDZ)
#
#  Step 2: Perform a calculation with basis set with cardinal number X+1 (here aug-cc-pVTZ)
#
#  Step 3: Perform a calculation with basis set with cardinal number X+2 (here aug-cc-pVQZ)
# 
#  Step 4: combine the energies to evaluate the CBS energy (E_CBS)
#
#  Step 5: Print the result
#
#
# NOTE 1: No empirical parameter is used here so one can change the method. Here we use 
#         the DLPNO-CCSD(T) method, because of it's efficiency/accuracy ratio.
#
# NOTE 2:  It worth mentioning that in the original publication Peterson et al. 
#          used mainly multireference MRCI and Full-CI methods so the protocol could 
#          be also appropriate for this kind of calculations
#
#
# ---------------------------------------------------------------
# --------------------  Variables  ------------------------------                
# ----------   Variables that could be adjusted using 'with'-----
Variable molecule;
Variable charge     = 0;
Variable mult       = 1;
Variable method     = "DLPNO-CCSD(T1)";
Variable myProperty ="MDCI_Total_Energy";
# -----------------  Rest of variables --------------------------
variable X, Y, Z;           # The cardinal numbers
variable E_DZ, E_TZ, E_QZ;  # The energies 
variable D;                 # Useful intermediate
variable E_CBS;             # The CBS energy

X = 2;
# ---------------------------------------------------------------
# (Calculation 1)
# Double zeta calculation 
# ---------------------------------------------------------------
NewStep
  !&{method} aug-cc-pVDZ aug-cc-pVDZ/C VeryTightSCF 
  *xyzfile &{charge} &{mult} &{molecule}
StepEnd
E_DZ.readProperty(propertyName=myProperty);     

# ---------------------------------------------------------------
# (Calculation 2)
# Triple zeta calculation 
# ---------------------------------------------------------------
NewStep
  !&{method} aug-cc-pVTZ aug-cc-pVTZ/C VeryTightSCF 
StepEnd
E_TZ.readProperty(propertyName=myProperty);     

# ---------------------------------------------------------------
# (Calculation 3)
# Quadruple zeta calculation 
# ---------------------------------------------------------------
NewStep
  !&{method} aug-cc-pVQZ aug-cc-pVQZ/C VeryTightSCF 
StepEnd
E_QZ.readProperty(propertyName=myProperty);

# ---------------------------------------------------------------
# Perform the extrapolation
# ---------------------------------------------------------------
D = (exp(-(X-1)^2)-exp(-X^2+1))/(exp(-(X+1)^2)-exp(-X^2-1));  
E_CBS = (e*E_TZ-E_DZ + D*(-exp(-1)*E_TZ+E_QZ))/((e-1)-D*(exp(-1)-1));  

print("\n\n--------------------------------------------------------------\n");
print("                    EXTRAPOLATION RESULTS                    \n");
print("           extrapolation scheme by Kirk A. Peterson et al.   \n");
print("  Kirk A. Peterson , David E. Woon, and Thorn H. Dunning, Jr.\n");
print("             J. Chem. Phys. 100 (10), (1994)\n");
print(" Molecule           : %s\n", molecule);
print(" Charge             : %18d\n", charge);
print(" Mult               : %18d\n", mult);
print(" Method             : %s\n", method);
print(" E(aug-cc-pVDZ)     : %18.8lf\n", E_DZ);
print(" E(aug-cc-pVTZ)     : %18.8lf\n", E_TZ);
print(" E(aug-cc-pVQZ)     : %18.8lf\n", E_QZ);
print(" CBS Energy         : %18.8lf\n", E_CBS);
print("--------------------------------------------------------------\n");
EndRun
