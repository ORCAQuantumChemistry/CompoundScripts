#
# XYG3 with RI-MP2.
#
# Final energy : EDH
#

Variable molecule;
Variable charge = 0;
Variable mult = 1;
Variable xc = "xyg3";
Variable basis = "def2-svp";
Variable auxbas = "def2-svp/c";
Variable dlpno = "false";

Variable acm[3] ;
Variable scalldac = 0.6789;
Variable pt2_coeff = 0.3211;
Variable Enscf;
Variable Ept2;
Variable Edh;

if (xc = "xyg3") then
 pt2_coeff = 0.3211;
 acm[0] = 0.8033;
 acm[1] = 0.2107;
 acm[2] = 0.6789;
 scalldac = 0.6789;
endif

NewStep
  !b3lyp_g &{basis}
  !noautostart
  *xyzfile &{charge} &{mult} &{molecule}
StepEnd
Alias scf

ReadMOs(scf);
NewStep
  !b3lyp_g &{basis}
  !nopop 
  %scf 
    maxiter 1 
    ignoreconv true
  end
  %method
    Functional b3lyp_g
    ACM = &{acm[0]}, &{acm[1]},&{acm[2]}
    ScalLDAC = &{scalldac}
  end
StepEnd
Enscf.ReadProperty(propertyName="SCF_Energy");

ReadMOs(scf);
NewStep
  !RI-MP2 &{basis} &{auxbas}
  !noiter nopop
  !nofrozencore
  %mp2
    dlpno &{dlpno}
  end
StepEnd
Ept2.ReadProperty(propertyName="MP2_CORR_ENERGY");

Edh = (Enscf + Ept2*pt2_coeff);

print("-------------   Results  ----------------\n");
print("pt2 coef  : %20.8lf\n", pt2_coeff);
print("Enscf     : %20.8lf\n", Enscf);
print("Ept2      : %20.8lf\n", Ept2);
print("Edh       : %20.8lf\n", Edh);

EndRun
